<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml"><head><title>index (bitwuzla.index)</title><link rel="stylesheet" href="../static/css/theme.css" type="text/css" /><link rel="stylesheet" href="static/css/odoc.css"/><link rel="stylesheet" href="static/css/custom.css"/><meta charset="utf-8"/><meta name="generator" content="odoc 1.5.2"/><meta name="viewport" content="width=device-width,initial-scale=1.0"/><script src="static/js/highlight.pack.js"></script><script>hljs.initHighlightingOnLoad();</script></head><body><div class="content"><header><nav><a href="../index.html" class="icon icon-home">&nbsp;Bitwuzla Documentation</a> â€“ Bitwuzla Ocaml API Bindings<nav class="toc"><ul><li><a href="#bitwuzla-ocaml-api">Bitwuzla Ocaml API</a><ul><li><a href="#quickstart">Quickstart</a></li><li><a href="#examples">Examples</a><ul><li><a href="#quickstart-example">Quickstart example</a></li><li><a href="#incremental-example-with-push-and-pop">Incremental example with push and pop</a></li><li><a href="#incremental-example-with-check-sat-assuming">Incremental example with check-sat-assuming</a></li><li><a href="#unsat-core-example">Unsat core example</a></li><li><a href="#unsat-assumptions-example">Unsat assumptions example</a></li></ul></li><li><a href="#interface">Interface</a></li></ul></li></ul></nav></header><h2 id="bitwuzla-ocaml-api"><a href="#bitwuzla-ocaml-api" class="anchor"></a>Bitwuzla Ocaml API</h2><p>Bitwuzla is an SMT solver for QF_AUFBVFP problems.</p><h3 id="quickstart"><a href="#quickstart" class="anchor"></a>Quickstart</h3><p>You will want to create some expressions and assert formulas. For example, consider the following SMT-LIB input:</p><pre>(set-logic QF_BV)
(set-option :produce-models true)
(declare-const x (_ BitVec 8))
(declare-const y (_ BitVec 8))
(assert
    (distinct
        ((_ extract 3 0) (bvsdiv x (_ bv2 8)))
        ((_ extract 3 0) (bvashr y (_ bv1 8)))))
(check-sat)
(get-model)
(exit)</pre><p>This input is created and asserted as follows:</p><pre><code class="ml">(* First, create a Bitwuzla instance. *)
let open Bitwuzla.Once () in
(* Create a bit-vector sort of size 8. *)
let bv8 = Sort.bv 8 in
(* Create two bit-vector variables of that sort. *)
let x = Term.const bv8 &quot;x&quot; and y = Term.const bv8 &quot;y&quot; in
(* Create bit-vector values one and two of the same sort. *)
let one = Term.Bv.one bv8 and two = Term.Bv.of_int bv8 2 in
(* (bvsdiv x (_ bv2 8)) *)
let sdiv = Term.Bv.sdiv x two in
(* (bvashr y (_ bv1 8)) *)
let ashr = Term.Bv.shift_right y one in
(* ((_ extract 3 0) (bvsdiv x (_ bv2 8))) *)
let sdive = Term.Bv.extract ~hi:3 ~lo:0 sdiv in
(* ((_ extract 3 0) (bvashr x (_ sortbv1 8))) *)
let ashre = Term.Bv.extract ~hi:3 ~lo:0 ashr in
(*
   (assert
     (distinct
       ((_ extract 3 0) (bvsdiv x (_ sortbv2 8)))
       ((_ extract 3 0) (bvashr y (_ sortbv1 8)))))
*)
assert' @@ Term.equal sdive ashre;</code></pre><p>After asserting formulas, satisfiability can be determined via <code>check_sat</code>.</p><pre><code class="ml">(* (check-sat) *)
let result = check_sat () in</code></pre><p>If the formula is satifiable, it is possible to query the value of expressions via <code>get_value</code> as well as its concrete value via <code>assignment</code>.</p><pre><code class="ml">assert (result = Sat);
let xval = get_value x and yval = get_value y in
Format.printf &quot;assignment of x: %s@\n&quot;
@@ Z.format &quot;%08b&quot; @@ Term.Bv.assignment xval;
Format.printf &quot;assignment of y: %s@\n&quot;
@@ Z.format &quot;%08b&quot; @@ Term.Bv.assignment yval;</code></pre><p>It is also possible to query the model value of expressions that do not occur in the input formula.</p><pre><code class="ml">let x2 = Term.Bv.mul x x in
let x2val = get_value x2 in
Format.printf &quot;assignment of x * x: %s@\n&quot;
@@ Z.format &quot;%08b&quot; @@ Term.Bv.assignment x2val;</code></pre><p>We can then let the garbage collector delete the Bitwuzla instance, but if we want to release the resources earlier, it is possible to call the funcion <code>unsafe_close ()</code> as the last action of the session.</p><pre><code class="ml">unsafe_close ()</code></pre><h3 id="examples"><a href="#examples" class="anchor"></a>Examples</h3><p>All examples can be found in directory <a href="https://github.com/bitwuzla/ocaml-bitwuzla/tree/master/examples">examples</a>. Assuming Bitwuzla was built from sources, run the following command to build and run an example:</p><p><code>dune exec -- examples/quickstart.exe # replace quickstart by other examples</code></p><h4 id="quickstart-example"><a href="#quickstart-example" class="anchor"></a>Quickstart example</h4><p>The example used in the <a href="index.html#quickstart"><span>Quickstart</span></a> guide.</p><p>The SMT-LIB input for this example can be found at <a href="https://github.com/bitwuzla/ocaml-bitwuzla/tree/master/examples/quickstart.smt2">examples/quickstart.smt2</a>. The source code for this example can be found at <a href="https://github.com/bitwuzla/ocaml-bitwuzla/tree/master/examples/quickstart.ml">examples/quickstart.ml</a>.</p><pre><code class="ml">let () =

  (* First, create a Bitwuzla instance. *)
  let open Bitwuzla.Once () in

  (* Create a bit-vector sort of size 8. *)
  let bv8 = Sort.bv 8 in

  (* Create two bit-vector variables of that sort. *)
  let x = Term.const bv8 &quot;x&quot; and y = Term.const bv8 &quot;y&quot; in

  (* Create bit-vector values one and two of the same sort. *)
  let one = Term.Bv.one bv8 and two = Term.Bv.of_int bv8 2 in

  (* (bvsdiv x (_ bv2 8)) *)
  let sdiv = Term.Bv.sdiv x two in

  (* (bvashr y (_ bv1 8)) *)
  let ashr = Term.Bv.shift_right y one in

  (* ((_ extract 3 0) (bvsdiv x (_ bv2 8))) *)
  let sdive = Term.Bv.extract ~hi:3 ~lo:0 sdiv in

  (* ((_ extract 3 0) (bvashr x (_ sortbv1 8))) *)
  let ashre = Term.Bv.extract ~hi:3 ~lo:0 ashr in

  (*
     (assert
       (distinct
         ((_ extract 3 0) (bvsdiv x (_ sortbv2 8)))
         ((_ extract 3 0) (bvashr y (_ sortbv1 8)))))
  *)
  assert' @@ Term.equal sdive ashre;

  (* (check-sat) *)
  let result = check_sat () in
  assert (result = Sat);

  (* (get-model) *)
  let xval = get_value x and yval = get_value y in
  Format.printf &quot;assignment of x: %s@\n&quot; @@ Z.format &quot;%08b&quot;
  @@ Term.Bv.assignment xval;
  Format.printf &quot;assignment of y: %s@\n&quot; @@ Z.format &quot;%08b&quot;
  @@ Term.Bv.assignment yval;
  let x2 = Term.Bv.mul x x in
  let x2val = get_value x2 in
  Format.printf &quot;assignment of x * x: %s@\n&quot; @@ Z.format &quot;%08b&quot;
  @@ Term.Bv.assignment x2val;

  (* Finally, delete the Bitwuzla instance. *)
  unsafe_close ()</code></pre><h4 id="incremental-example-with-push-and-pop"><a href="#incremental-example-with-push-and-pop" class="anchor"></a>Incremental example with push and pop</h4><p>An incremental example with <code>push</code> and <code>pop</code>.</p><p>The SMT-LIB input for this example can be found at <a href="https://github.com/bitwuzla/ocaml-bitwuzla/tree/master/examples/pushpop.smt2">examples/pushpop.smt2</a>. The source code for this example can be found at <a href="https://github.com/bitwuzla/ocaml-bitwuzla/tree/master/examples/pushpop.ml">examples/pushpop.ml</a>.</p><pre><code class="ml">let () =

  (* First, create a Bitwuzla instance enabling incremental solving. *)
  let open Bitwuzla.Incremental () in

  (* Create a bit-vector sort of size 1 and another of size 2. *)
  let bv1 = Sort.bv 1 and bv2 = Sort.bv 2 in

  (* (declare-const o0 (_ BitVec 1)) *)
  let o0 = Term.const bv1 &quot;o0&quot;
  (* (declare-const o1 (_ BitVec 1)) *)
  and o1 = Term.const bv1 &quot;o1&quot;
  (* (declare-const s0 (_ BitVec 2)) *)
  and s0 = Term.const bv2 &quot;s0&quot;
  (* (declare-const s1 (_ BitVec 2)) *)
  and s1 = Term.const bv2 &quot;s1&quot;
  (* (declare-const s2 (_ BitVec 2)) *)
  and s2 = Term.const bv2 &quot;s2&quot;
  (* (declare-const goal (_ BitVec 2)) *)
  and goal  = Term.const bv2 &quot;goal&quot;

  (* Create bit-vector values zero, one, three. *)
  and zero  = Term.Bv.zero bv2
  and one2  = Term.Bv.one bv2
  and three = Term.Bv.of_int bv2 3 in

  (* Add some assertions. *)
  assert' @@ Term.equal s0 zero;
  assert' @@ Term.equal goal three;

  (* Push, assert, check sat and pop. *)
  push 1;
  assert' @@ Term.equal s0 goal;
  Format.printf &quot;Expect: unsat@\n&quot;;
  Format.printf &quot;Bitwuzla: %a@\n&quot; pp_result @@ check_sat ();
  pop 1;

  (* (assert (= s1 (ite (= o0 (_ sortbv1 1)) (bvadd s0 one) s0))) *)
  assert' @@ Term.equal s1 (Term.ite o0 (Term.Bv.add s0 one2) s0);

  (* Push, assert, check sat and pop. *)
  push 1;
  assert' @@ Term.equal s1 goal;
  Format.printf &quot;Expect: unsat@\n&quot;;
  Format.printf &quot;Bitwuzla: %a@\n&quot; pp_result @@ check_sat ();
  pop 1;

  (* (assert (= s2 (ite (= o1 (_ sortbv1 1)) (bvadd s1 one) s1))) *)
  assert' @@ Term.equal s2 (Term.ite o1 (Term.Bv.add s1 one2) s1);

  (* Push, assert, check sat and pop. *)
  push 1;
  assert' @@ Term.equal s1 goal;
  Format.printf &quot;Expect: unsat@\n&quot;;
  Format.printf &quot;Bitwuzla: %a@\n&quot; pp_result @@ check_sat ();
  pop 1;

  (* Finally, delete the Bitwuzla instance. *)
  unsafe_close ()</code></pre><h4 id="incremental-example-with-check-sat-assuming"><a href="#incremental-example-with-check-sat-assuming" class="anchor"></a>Incremental example with check-sat-assuming</h4><p>This example shows how to implement the example above with <code>check-sat-assuming</code> instead of <code>push</code> and <code>pop</code>.</p><p>The SMT-LIB input for this example can be found at <a href="https://github.com/bitwuzla/ocaml-bitwuzla/tree/master/examples/checksatassuming.smt2">examples/checksatassuming.smt2</a>. The source code for this example can be found at <a href="https://github.com/bitwuzla/ocaml-bitwuzla/tree/master/examples/checksatassuming.ml">examples/checksatassuming.ml</a>.</p><pre><code class="ml">let () =

  (* First, create a Bitwuzla instance enabling incremental solving. *)
  let open Bitwuzla.Incremental () in

  (* Create a bit-vector sort of size 1 and another of size 2. *)
  let bv1 = Sort.bv 1 and bv2 = Sort.bv 2 in

  (* (declare-const o0 (_ BitVec 1)) *)
  let o0 = Term.const bv1 &quot;o0&quot;
  (* (declare-const o1 (_ BitVec 1)) *)
  and o1 = Term.const bv1 &quot;o1&quot;
  (* (declare-const s0 (_ BitVec 2)) *)
  and s0 = Term.const bv2 &quot;s0&quot;
  (* (declare-const s1 (_ BitVec 2)) *)
  and s1 = Term.const bv2 &quot;s1&quot;
  (* (declare-const s2 (_ BitVec 2)) *)
  and s2 = Term.const bv2 &quot;s2&quot;
  (* (declare-const goal (_ BitVec 2)) *)
  and goal  = Term.const bv2 &quot;goal&quot;

  (* Create bit-vector values zero, one, three. *)
  and zero  = Term.Bv.zero bv2
  and one2  = Term.Bv.one bv2
  and three = Term.Bv.of_int bv2 3 in

  (* Add some assertions. *)
  assert' @@ Term.equal s0 zero;
  assert' @@ Term.equal goal three;

  (* (check-sat-assuming ((= s0 goal))) *)
  Format.printf &quot;Expect: unsat@\n&quot;;
  Format.printf &quot;Bitwuzla: %a@\n&quot; pp_result
  @@ check_sat_assuming [| Term.equal s0 goal |];

  (* (assert (= s1 (ite (= o0 (_ sortbv1 1)) (bvadd s0 one) s0))) *)
  assert' @@ Term.equal s1 (Term.ite o0 (Term.Bv.add s0 one2) s0);

  (* (check-sat-assuming ((= s1 goal))) *)
  Format.printf &quot;Expect: unsat@\n&quot;;
  Format.printf &quot;Bitwuzla: %a@\n&quot; pp_result
  @@ check_sat_assuming [| Term.equal s1 goal |];

  (* (assert (= s2 (ite (= o1 (_ sortbv1 1)) (bvadd s1 one) s1))) *)
  assert' @@ Term.equal s2 (Term.ite o1 (Term.Bv.add s1 one2) s1);

  (* (check-sat-assuming ((= s2 goal))) *)
  Format.printf &quot;Expect: unsat@\n&quot;;
  Format.printf &quot;Bitwuzla: %a@\n&quot; pp_result
  @@ check_sat_assuming [| Term.equal s2 goal |];

  (* Finally, delete the Bitwuzla instance. *)
  unsafe_close ()</code></pre><h4 id="unsat-core-example"><a href="#unsat-core-example" class="anchor"></a>Unsat core example</h4><p>This example shows how to extract an unsat core. It creates bit-vector and floating-point terms and further illustrates how to create lambda terms (<code>define-fun</code>).</p><p>The SMT-LIB input for this example can be found at <a href="https://github.com/bitwuzla/ocaml-bitwuzla/tree/master/examples/unsatcore.smt2">examples/unsatcore.smt2</a>. The source code for this example can be found at <a href="https://github.com/bitwuzla/ocaml-bitwuzla/tree/master/examples/unsatcore.ml">examples/unsatcore.ml</a>.</p><pre><code class="ml">let () =

  (* First, create a Bitwuzla instance enabling unsat core extraction. *)
  let open Bitwuzla.Unsat_core () in

  (* Create a bit-vector sort of size 2 and another of size 4. *)
  let bv2 = Sort.bv 2 and bv4 = Sort.bv 4
  (* Create Float16 floatinf-point sort. *)
  and fp16 = Sort.fp ~exponent:5 16 in

  (* Create bit-vector variables. *)
  (* (declare-const x0 (_ BitVec 4)) *)
  let x0 = Term.const bv4 &quot;x0&quot;
  (* (declare-const x1 (_ BitVec 2)) *)
  and x1 = Term.const bv2 &quot;x1&quot;
  (* (declare-const x2 (_ BitVec 2)) *)
  and x2 = Term.const bv2 &quot;x2&quot;
  (* (declare-const x3 (_ BitVec 2)) *)
  and x3 = Term.const bv2 &quot;x3&quot;
  (* (declare-const x4 Float16) *)
  and x4 = Term.const fp16 &quot;x4&quot; in

  (* Create FP positive zero. *)
  let fpzero = Term.Fp.pos_zero fp16
  (* Create BV zero of size 4. *)
  and bvzero = Term.Bv.zero bv4 in

  (* (define-fun f0 ((a Float16)) Bool (fp.gt a (_ +zero 5 11))) *)
  let f0 = Term.Uf.lambda [ fp16 ] (fun [ a ] -&gt; Term.Fp.gt a fpzero) in

  (* (define-fun f1 ((a Float16)) (_ BitVec 4) (ite (f0 a) x0 #b0000)) *)
  let f1 = Term.Uf.lambda [ fp16 ] (fun [ a ] -&gt;
      Term.ite (Term.Uf.apply f0 [ a ]) x0 bvzero) in

  (* (define-fun f2 ((a Float16)) (_ BitVec 2) ((_ extract 1 0) (f1 a))) *)
  let f2 = Term.Uf.lambda [ fp16 ] (fun [ a ] -&gt;
      Term.Bv.extract ~hi:1 ~lo:0 (Term.Uf.apply f1 [ a ])) in

  (* (assert (! (bvult x2 (f2 (_ +zero 5 11))) :named assertion0)) *)
  assert' ~name:&quot;assertion0&quot;
  @@ Term.Bv.ult x2 (Term.Uf.apply f2 [ fpzero ]);

  (* (assert (! (= x1 x2 x3) :named assertion1)) *)
  assert' ~name:&quot;assertion1&quot;
  @@ Term.Bl.logand (Term.equal x1 x2) (Term.equal x2 x3);

  (* (assert (!(= x4 ((_ to_fp_unsigned 5 11) RNE x3)) :named assertion2)) *)
  assert' ~name:&quot;assertion2&quot;
  @@ Term.equal x4 (Term.Fp.of_sbv ~exponent:5 16 Term.Rm.rne x3);

  (* (check-sat) *)
  Format.printf &quot;Expect: unsat@\n&quot;;
  Format.printf &quot;Bitwuzla: %a@\n&quot; pp_result @@ check_sat ();

  (* (get-unsat-core) *)
  Format.printf &quot;Unsat core: {&quot;;
  Array.iter (fun a -&gt; Format.printf &quot; %a&quot; Term.pp a) @@ get_unsat_core ();
  Format.printf &quot; }@\n&quot;;

  (* Finally, delete the Bitwuzla instance. *)
  unsafe_close ()</code></pre><h4 id="unsat-assumptions-example"><a href="#unsat-assumptions-example" class="anchor"></a>Unsat assumptions example</h4><p>This example shows how to implement the example above with <code>get-unsat-assumptions</code>.</p><p>The SMT-LIB input for this example can be found at <a href="https://github.com/bitwuzla/ocaml-bitwuzla/tree/master/examples/unsatassumption.smt2">examples/unsatassumption.smt2</a>. The source code for this example can be found at <a href="https://github.com/bitwuzla/ocaml-bitwuzla/tree/master/examples/unsatassumption.ml">examples/unsatassumption.ml</a>.</p><pre><code class="ml">let () =

  (* First, create a Bitwuzla instance enabling unsat core extraction. *)
  let open Bitwuzla.Incremental () in

  (* Create a bit-vector sort of size 2 and another of size 4. *)
  let bv2 = Sort.bv 2 and bv4 = Sort.bv 4
  (* Create Float16 floatinf-point sort. *)
  and fp16 = Sort.fp ~exponent:5 16 in

  (* Create bit-vector variables. *)
  (* (declare-const x0 (_ BitVec 4)) *)
  let x0 = Term.const bv4 &quot;x0&quot;
  (* (declare-const x1 (_ BitVec 2)) *)
  and x1 = Term.const bv2 &quot;x1&quot;
  (* (declare-const x2 (_ BitVec 2)) *)
  and x2 = Term.const bv2 &quot;x2&quot;
  (* (declare-const x3 (_ BitVec 2)) *)
  and x3 = Term.const bv2 &quot;x3&quot;
  (* (declare-const x4 Float16) *)
  and x4 = Term.const fp16 &quot;x4&quot; in

  (* Create FP positive zero. *)
  let fpzero = Term.Fp.pos_zero fp16
  (* Create BV zero of size 4. *)
  and bvzero = Term.Bv.zero bv4 in

  (* (define-fun f0 ((a Float16)) Bool (fp.gt a (_ +zero 5 11))) *)
  let f0 = Term.Uf.lambda [ fp16 ] (fun [ a ] -&gt; Term.Fp.gt a fpzero) in

  (* (define-fun f1 ((a Float16)) (_ BitVec 4) (ite (f0 a) x0 #b0000)) *)
  let f1 = Term.Uf.lambda [ fp16 ] (fun [ a ] -&gt;
      Term.ite (Term.Uf.apply f0 [ a ]) x0 bvzero) in

  (* (define-fun f2 ((a Float16)) (_ BitVec 2) ((_ extract 1 0) (f1 a))) *)
  let f2 = Term.Uf.lambda [ fp16 ] (fun [ a ] -&gt;
      Term.Bv.extract ~hi:1 ~lo:0 (Term.Uf.apply f1 [ a ])) in

  (* (define-fun assumption0 () Bool (bvult x2 (f2 (_ +zero 5 11)))) *)
  let assumption0 = Term.Bv.ult x2 (Term.Uf.apply f2 [ fpzero ]) in

  (* (define-fun assumption1 () Bool (= x1 x2 x3)) *)
  let assumption1 = Term.Bl.logand (Term.equal x1 x2) (Term.equal x2 x3) in

  (* (define-fun assumption2 () Bool (= x4 ((_ to_fp_unsigned 5 11) RNE x3))) *)
  let assumption2 =
    Term.equal x4 (Term.Fp.of_sbv ~exponent:5 16 Term.Rm.rne x3) in

  (* (check-sat) *)
  Format.printf &quot;Expect: unsat@\n&quot;;
  Format.printf &quot;Bitwuzla: %a@\n&quot; pp_result
  @@ check_sat_assuming
    ~names:[| &quot;assumption0&quot;; &quot;assumption1&quot;; &quot;assumption2&quot; |]
    [| assumption0; assumption1; assumption2 |];

  (* (get-unsat-core) *)
  Format.printf &quot;Unsat core: {&quot;;
  Array.iter (fun a -&gt; Format.printf &quot; %a&quot; Term.pp a)
  @@ get_unsat_assumptions ();
  Format.printf &quot; }@\n&quot;;

  (* Finally, delete the Bitwuzla instance. *)
  unsafe_close ()</code></pre><h3 id="interface"><a href="#interface" class="anchor"></a>Interface</h3><p>The <a href="Bitwuzla/index.html"><code>Bitwuzla</code></a> library proposes the following sessions:</p><ul><li><a href="Bitwuzla/Once/index.html"><code>Bitwuzla.Once</code></a> (for one time check and model query);</li><li><a href="Bitwuzla/Incremental/index.html"><code>Bitwuzla.Incremental</code></a> (for incremental solving);</li><li><a href="Bitwuzla/Unsat_core/index.html"><code>Bitwuzla.Unsat_core</code></a> (for unsatifiable core generation).</li></ul></div></body></html>
